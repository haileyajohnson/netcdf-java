apply plugin: 'java'
apply from: "$rootDir/gradle/any/properties.gradle"

tasks.withType(Test).all {
  // Propagates system properties set on the Gradle process to the test executors.
  addFilteredSysProps(systemProperties)

  if (isJenkins && !isRelease) {
    // On Jenkins, don't let test failures fail the build unless we are doing we release; we want
    // the full test report.
    ignoreFailures = true
  } else {
    // Otherwise, fail the build at the first sign of failure.
    ignoreFailures = false
  }

  useJUnit {
    // if we are not explicitly trying to run all tests, allow some categories to be ignored
    if (!runAllTests) {
      if (isJenkins) {
        excludeCategories 'ucar.unidata.util.test.category.NotJenkins'
      }

      if (isPullRequestCheck) {
        executable = new File("${javaTestVersion}", 'bin/java')
        excludeCategories 'ucar.unidata.util.test.category.NotPullRequest'
        excludeCategories 'ucar.unidata.util.test.category.NeedsExternalResource'
      }

      if (!isContentRootAvailable && !isJenkins) { // Don't skip tests on Jenkins, except NotJenkins ones.
        excludeCategories 'ucar.unidata.util.test.category.NeedsContentRoot'
      }

      if (!isCdmUnitTestDirAvailable && !isJenkins) {  // Don't skip tests on Jenkins, except NotJenkins ones.
        excludeCategories 'ucar.unidata.util.test.category.NeedsCdmUnitTest'
      }

      if (!isRdaDataAvailable) {
        excludeCategories 'ucar.unidata.util.test.category.NeedsRdaData'
      }

      if (!runSlowTests) {
        excludeCategories 'ucar.unidata.util.test.category.Slow'
      }

      if (!isUcarNetworkAvailable) {
        excludeCategories 'ucar.unidata.util.test.category.NeedsUcarNetwork'
      }
    }
  }
}
